locals {
  app_sa_roles = [
    "roles/cloudprofiler.agent",            # Profiling
    "roles/cloudtrace.agent",               # Tracing for monitoring
    "roles/monitoring.editor",              # Exporting metrics
    "roles/iam.serviceAccountTokenCreator", # For signing JWTs
  ]
}

resource "google_service_account" "app" {
  count = var.enable && contains(["default", "preview_shared"], var.env_type) ? 1 : 0

  provider     = google.target
  project      = var.google_project
  account_id   = "${local.service}-${local.owner}"
  display_name = "${local.service}-${local.owner}"
}

resource "google_project_iam_member" "app" {
  count    = var.enable && contains(["default", "preview_shared"], var.env_type) ? length(local.app_sa_roles) : 0
  provider = google.target
  project  = var.google_project
  role     = local.app_sa_roles[count.index]
  member   = "serviceAccount:${google_service_account.app[0].email}"
}